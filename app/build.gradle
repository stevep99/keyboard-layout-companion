apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 30

    defaultConfig {
        applicationId "io.github.colemakmods.keyboard_companion"
        minSdkVersion 21
        targetSdkVersion 30

        versionCode 4
        versionName "1.02"
        setProperty("archivesBaseName", "companion-$versionName")
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    allprojects {
        afterEvaluate { project ->
            def propsFile = rootProject.file('keystore.properties')
            def configName = 'release'

            if (propsFile.exists() && android.signingConfigs.hasProperty(configName)) {
                def props = new Properties()
                props.load(new FileInputStream(propsFile))
                android.signingConfigs[configName].storeFile = file(props['storeFile'])
                android.signingConfigs[configName].storePassword = props['storePassword']
                android.signingConfigs[configName].keyAlias = props['keyAlias']
                android.signingConfigs[configName].keyPassword = props['keyPassword']
            }
        }
    }

    signingConfigs {
        release {
            storeFile file('storeFile')
            storePassword ''
            keyAlias ''
            keyPassword ''
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFile getDefaultProguardFile('proguard-android.txt')
            proguardFile 'proguard-rules.cfg'
            signingConfig signingConfigs.release
        }
    }
}

repositories {
    google()
    mavenCentral()
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation  group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.10.3'
    implementation  group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.10.3'
    implementation  group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.10.3'
    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation "androidx.core:core-ktx:1.5.0"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
}
